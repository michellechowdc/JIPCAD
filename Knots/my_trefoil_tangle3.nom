# my_trefoil_tangle3.nom
# remaking it centered at 0
# 
# Michelle CHow  2024/11/7
#####################################################

##############  surface colors   ########################### 
surface W   color (0.9 0.9 0.9) endsurface   # White
surface D   color (0.6 0.6 0.6) endsurface   # Dark
surface K   color (0   0   0  ) endsurface   # blacK

surface O  color  (0.9 0.3 0  ) endsurface   # Orange
surface R  color  (1   0.1 0  ) endsurface   # Red
surface G  color  (0   0.9 0  ) endsurface   # Green
surface B  color  (0   0.3 1  ) endsurface   # Blue

backgroundcolor (0.6 0.7 0.8) endbackgroundcolor 

###################  Coordinate axes  ######################
point ooo  ( 0 0 0 ) endpoint 
point xoo  ( 1 0 0 ) endpoint
point oyo  ( 0 1 0 ) endpoint
point ooz  ( 0 0 1 ) endpoint

polyline xaxis (ooo xoo) endpolyline
polyline yaxis (ooo oyo) endpolyline
polyline zaxis (ooo ooz) endpolyline

group coord_axes   
  instance ix xaxis surface R   endinstance     
  instance iy yaxis surface B   endinstance
  instance iz zaxis surface G   endinstance
endgroup

instance  icax  coord_axes   endinstance

#####################  adjustable parameters  #################
bank p  
    # adjust radius of crosssections for sweep paths
    set tet_thickness    0.01   0.01  1   0.01    # more specifically, adjust radius of control sweep used for tetrahedron path
    set tref_thickness    0.14    0.01  1   0.01    # radius of control sweep used for trefoil sweep path

    # adjust number of spline segments used for sweep path, aka more segments means smoother sweep
    set spline_seg    60     24   600  12

    # adjust coordinates of trefoil sweep path
    set tref_middleCoord 1  0.1 5  0.1
    set tref_cornerCoord 1  0.1 5  0.1 
    set tref_width  0.5  0.05 1  0.01

    # experimenting to orient the new trefoils now
    # rotate itself about z axis
    ###try this first, but understand his tetra tangle example
    set trefxy_rotz  66   -360 360 1
    set tref_dist  0.0    -0.5 0.5 0.01 # distance of all from origin

    set temp_dist  0.60    -1 1 0.1 # distance of all from origin
    set temp_rot  -31   -360 360 1

    list ( tet_thickness tref_thickness   spline_seg   tref_middleCoord tref_cornerCoord tref_width   trefxy_rotz tref_dist  temp_dist temp_rot )
endbank

#####################  wire cube  #################
point Yxyz  ( 4  4  4 ) endpoint
point Yxyc  ( 4  4  0 ) endpoint
point Yxbc  ( 4 0 0 ) endpoint
point Yayz  (0  4  4 ) endpoint

polyline edge3  (Yxbc Yxyc Yxyz Yayz) endpolyline
#instance line edge3 surface D endinstance

group wcube
  instance ie1 edge3   translate (-2 -1.155 0) endinstance # offset y by 1.155 instead of 2 bc trefoil's center is not exactly at 2
  instance ie2 edge3   rotate(0 0 1)(90) translate (2 -1.155 0)  endinstance
  instance ie3 edge3   rotate(0 0 1)(180) translate (2 2.845 0)  endinstance # for same reasoning as above^, offset by 2 + (2-1.155) = 2.845
  instance ie4 edge3   rotate(0 0 1)(270) translate (-2 2.845 0)  endinstance
endgroup

#instance icube wcube  surface D endinstance 

#####################  tetrahedron frame  #################
point t1 (0 0 0) endpoint
point t2 (4 0 0) endpoint
point t3 (2 3.464 0) endpoint
point t4 (2 1.155 3.266) endpoint

polyline tetrahedronPath (t1 t2 t3 t1 t4 t3 t2 t4) endpolyline
#instance itet tetrahedronPath  surface k  translate (-2 -1.155 0)  endinstance 

circle ctprof  ( {expr $p.tet_thickness}  3 )  endcircle
sweep tetworm  crosssection  ctprof  endcrosssection  path tetrahedronPath  mintorsion  endpath   endsweep

instance tetInst tetworm  translate (-2 -1.155 0) endinstance # translate by -1.155 to make centered around origin bc tet frame is not exactly at 2

#####################  equilateral trefoil  #################
##### side length of 4
point ec1 (0 0 0) endpoint
point ec2 ({expr 4*$p.tref_cornerCoord} 0 0) endpoint
point ec3 ({expr 2*$p.tref_cornerCoord} {expr 3.464101615137*$p.tref_cornerCoord} 0) endpoint

point em1f ({expr 2*$p.tref_middleCoord} 0 {expr $p.tref_width}) endpoint
point em1b ({expr 2*$p.tref_middleCoord} 0 {expr -1*$p.tref_width}) endpoint
point em2f ({expr 3*$p.tref_middleCoord} {expr 1.73205080756*$p.tref_middleCoord} {expr $p.tref_width}) endpoint
point em2b ({expr 3*$p.tref_middleCoord} {expr 1.73205080756*$p.tref_middleCoord} {expr -1*$p.tref_width}) endpoint
point em3f ({expr 1*$p.tref_middleCoord} {expr 1.73205080756*$p.tref_middleCoord} {expr $p.tref_width}) endpoint
point em3b ({expr 1*$p.tref_middleCoord} {expr 1.73205080756*$p.tref_middleCoord} {expr -1*$p.tref_width}) endpoint

bspline  trefpath  order 4  (ec1 em1f em2b ec3 em3f em1b ec2 em2f em3b    ec1 em1f em2b)  segs {expr $p.spline_seg}   endbspline

circle bsprof  ( {expr $p.tref_thickness}  9 )  endcircle
sweep trefworm  crosssection  bsprof  endcrosssection    path trefpath  mintorsion  endpath   endsweep

#instance trefInst trefworm  translate (-2 -1.155 0) endinstance

#####################  trying trefoil tangle now #################
# interesting result from trefxy_rotz = 66, temp_dist = 0.60, temp_rot = -31

group tangle
  instance il1 trefworm  surface B  translate(-2 -1.155 0)  rotate(0 0 1)({expr $p.trefxy_rotz})  translate(0 0 {expr -1*$p.tref_dist}) translate(0 0 {expr $p.temp_dist}) rotate(0 0 1)({expr $p.temp_rot})  endinstance   
  instance il2 trefworm  surface R  translate(-2 -1.155 0)  rotate(0 0 1)({expr $p.trefxy_rotz})  rotate(1 0 0)(90) rotate(1 0 0)(-30) translate(0 -1.155 1.633)  translate(0 {expr -1*$p.tref_dist} {expr $p.tref_dist})  endinstance  
  instance il3 trefworm  surface G  translate(-2 -1.155 0)  rotate(0 0 1)({expr $p.trefxy_rotz})  rotate(1 0 0)(90) rotate(1 0 0)(-30) translate(0 -1.155 1.633) rotate(0 0 1)(120)  translate({expr $p.tref_dist} {expr $p.tref_dist} {expr $p.tref_dist})  endinstance  
  instance il4 trefworm  surface W  translate(-2 -1.155 0)  rotate(0 0 1)({expr $p.trefxy_rotz})  rotate(1 0 0)(90) rotate(1 0 0)(-30) translate(0 -1.155 1.633) rotate(0 0 1)(240)  translate({expr -1*$p.tref_dist} {expr $p.tref_dist} {expr $p.tref_dist})  endinstance  
  # order of transformation: (center tref about origin) (parameter to rotate tref about own z axis) (rotate up out of xy plane) (rotate to follow angle of tet face) (translate into location of tet face) 
  # translate up by half of z-height of tetrahedron 3.266 / 2 = 1.633
endgroup

instance itan tangle endinstance

#### NEXT: rotate trefoil up out of xy plane and save in this orientation? then replicate and rotate about z axis again to get the other 3