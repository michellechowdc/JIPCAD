## New_Disk(5,3).nom
#
## A SoapFilm between a Circle and a TorusKnot(5,3)
#
### >>>  For better viewing:
## in Render_Config:     Add WireFrame, NO FrontFace
## in Selection_Config:  Enable Vertex Selection 

## >>> Experiment with this!
##  >> Turn off some "skirts" or "crosses" and then recreate them interactively.
#  Make a more interesting "pentafoil" toplace in disk and in leaf
#
##  CHS  >>> Updated: 2023/12/1
#

############### Some Surface colors ##################
surface Z color (1 0 0.5) endsurface        # Zinnober
surface R color (1 0 0) endsurface          # Red
surface O color (0.9 0.6 0) endsurface      # Orange
surface Y color (1 1 0) endsurface          # Yellow
surface L color (0.6 0.8 0) endsurface      # Lime
surface G color (0 0.9 0) endsurface        # Green
surface A color (0 0.9 0.7) endsurface      # Aqua
surface C color (0 1 1) endsurface          # Cyan
surface U color (0 0.5 1) endsurface        # Uniform
surface B color (0 0 1) endsurface          # Blue
surface P color (0.5 0 1) endsurface        # Purple
surface M color (1 0 1) endsurface          # Magenta
surface W color (1 1 1) endsurface          # White
surface S color (0.8 0.8 0.8) endsurface    # Snow(dirty)

backgroundcolor  (0.7 0.7 0.7)  endbackgroundcolor


##################################################################################
(*  >>> Work on this later !

subdivision subdiv  type SLF_CATMULL_CLARK   subdivisions 3   endsubdivision

offset  fatty       type WEIGHTED     (thick, width)    endoffset
*)
##################################################################################

bank p  ## all paramaeters
  set	rimnum	55	1      20      5	# number of segments		
  set	rimrad	 4	2      20      0.1	# rim circle radius		
  set	rimrot	 0    -60      60      1	# rotate outer circle 		
 
  set   prad     5.3    1      20      0.1      #  pentafoil.radius   
  set   irad     2      1      20      0.1      #  pentafoil.inner ctrl pts rad  
  set   psep    20      0      30      1        #  pentafoil.sep.angle
  set   ptik     2.5   -5       5      0.1      #  pentafoil.zthick
  set   pseg    90     15      90      5        #  pentafoil.segments

  list ( rimnum rimrad rimrot   prad irad psep ptik pseg )
endbank

################  Border curves  ##################################################################

circle circ  ( {expr $p.rimrad} {expr $p.rimnum} ) endcircle 

instance  co  circ    rotate (0 0 1)( {expr $p.rimrot} )  surface R  endinstance    ## Outer Circle


point center ( 0 0 0) endpoint

point b0 ( {expr $p.prad*cos(0.0174533*( $p.psep))}      {expr $p.prad*sin(0.0174533*( $p.psep))}      {expr  $p.ptik} ) endpoint
point i0 ( {expr $p.irad*cos(0.0174533*( 108))}          {expr $p.irad*sin(0.0174533*( 108))}          0 ) endpoint
point b1 ( {expr $p.prad*cos(0.0174533*(-$p.psep+216))}  {expr $p.prad*sin(0.0174533*(-$p.psep+216))}  {expr -$p.ptik} ) endpoint

point b2 ( {expr $p.prad*cos(0.0174533*( $p.psep+216))}  {expr $p.prad*sin(0.0174533*( $p.psep+216))}  {expr  $p.ptik} ) endpoint
point i2 ( {expr $p.irad*cos(0.0174533*( -36))}          {expr $p.irad*sin(0.0174533*( -36))}          0 ) endpoint
point b3 ( {expr $p.prad*cos(0.0174533*(-$p.psep+72))}   {expr $p.prad*sin(0.0174533*(-$p.psep+72))}   {expr -$p.ptik} ) endpoint

point b4 ( {expr $p.prad*cos(0.0174533*( $p.psep+72))}   {expr $p.prad*sin(0.0174533*( $p.psep+72))}   {expr  $p.ptik} ) endpoint
point i4 ( {expr $p.irad*cos(0.0174533*( 180))}          {expr $p.irad*sin(0.0174533*( 180))}          0 ) endpoint
point b5 ( {expr $p.prad*cos(0.0174533*(-$p.psep+288))}  {expr $p.prad*sin(0.0174533*(-$p.psep+288))}  {expr -$p.ptik} ) endpoint

point b6 ( {expr $p.prad*cos(0.0174533*( $p.psep+288))}  {expr $p.prad*sin(0.0174533*( $p.psep+288))}  {expr  $p.ptik} ) endpoint
point i6 ( {expr $p.irad*cos(0.0174533*( 36))}           {expr $p.irad*sin(0.0174533*( 36))}           0 ) endpoint
point b7 ( {expr $p.prad*cos(0.0174533*(-$p.psep+144))}  {expr $p.prad*sin(0.0174533*(-$p.psep+144))}  {expr -$p.ptik} ) endpoint

point b8 ( {expr $p.prad*cos(0.0174533*( $p.psep+144))}  {expr $p.prad*sin(0.0174533*( $p.psep+144))}   {expr  $p.ptik} ) endpoint
point i8 ( {expr $p.irad*cos(0.0174533*( -108))}         {expr $p.irad*sin(0.0174533*( -108))}          0 ) endpoint
point b9 ( {expr $p.prad*cos(0.0174533*(-$p.psep))}      {expr $p.prad*sin(0.0174533*(-$p.psep))}       {expr -$p.ptik} ) endpoint

# polyline ctrlpoly  ( b0 i0 b1  b2 i2 b3  b4 i4 b5  b6 i6 b7  b8 i8 b9  b0 )     endpolyline
# instance cp0  ctrlpoly   surface B   endinstance

bspline  bsp   order 4    ( b0 i0 b1  b2 i2 b3  b4 i4 b5  b6 i6 b7  b8 i8 b9  b0 i0 b1 )    segs  {expr $p.pseg}   endbspline

instance  pfoil  bsp  surface O    endinstance    ##  Inner CinqueFoil

 
#############  Filled-in soapfilm meshes:  Added interactively  ##################

mesh skirt
  face f0 (.co.v0 .co.v1  .pfoil.v82 .pfoil.v81 ) endface
  face f1 (.co.v1 .co.v2  .pfoil.v83 .pfoil.v82 ) endface
  face f2 (.co.v2 .co.v3  .pfoil.v84 .pfoil.v83 ) endface
  face f3 (.co.v3 .co.v4  .pfoil.v85 .pfoil.v84 ) endface
  face f4 (.co.v4 .co.v5  .pfoil.v86 .pfoil.v85 ) endface
  face f5 (.co.v5 .co.v6  .pfoil.v22 .pfoil.v86 ) endface
  face f6 (.co.v6 .co.v7  .pfoil.v23 .pfoil.v22 ) endface
  face f7 (.co.v7 .co.v8  .pfoil.v24 .pfoil.v23 ) endface
  face f8 (.co.v8 .co.v9  .pfoil.v25 .pfoil.v24 ) endface
  face f9 (.co.v9 .co.v10 .pfoil.v26 .pfoil.v25 ) endface
  face f10 (.co.v10 .co.v11 .pfoil.v27 .pfoil.v26 ) endface
endmesh

instance sC  skirt   surface C   endinstance
instance sY  skirt   surface Y   rotate(0 0 1)(72)   endinstance
instance sO  skirt   surface G   rotate(0 0 1)(144)   endinstance
instance sR  skirt   surface R   rotate(0 0 1)(216)   endinstance
instance sM  skirt   surface M   rotate(0 0 1)(288)   endinstance   

mesh cross
  face f0 (.pfoil.v86 .pfoil.v87 .pfoil.v21 .pfoil.v22 ) endface
  face f1 (.pfoil.v87 .pfoil.v88 .pfoil.v20 .pfoil.v21 ) endface
  face f2 (.pfoil.v88 .pfoil.v89 .pfoil.v55 .pfoil.v54 ) endface
  face f3 (.pfoil.v54 .pfoil.v53 .pfoil.v19 .pfoil.v20 ) endface
  face f4 (.pfoil.v20 .pfoil.v88 .pfoil.v54 ) endface
endmesh

instance mC  cross   surface C     endinstance   #>>> these should be "reverse" -- Need to activate that option again.
instance mY  cross   surface Y   rotate(0 0 1)(72)   endinstance
instance mO  cross   surface G   rotate(0 0 1)(144)   endinstance
instance mR  cross   surface R   rotate(0 0 1)(216)   endinstance
instance mM  cross   surface M   rotate(0 0 1)(288)   endinstance


###################  END  ###########################################

